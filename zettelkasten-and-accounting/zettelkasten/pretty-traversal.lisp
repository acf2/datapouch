;;;; pretty-traversal.lisp


(in-package :zac.zettelkasten.pretty-traversal)


;;; fields is a function of one argument - how long join-chain is supposed to be in this select
(defun build-select-notes-through-links (&key ((:backward backward?) nil)
                                              ((:depth exponent) 1)
                                              ((:closure closure?) nil)
                                              ((:link-name-generator link-name-generator) (lambda (&optional index)
                                                                                            (make-name :table :link
                                                                                                       :index index)))
                                              ((:starting-table-alias starting-table-alias) :source)
                                              ((:ending-table-alias ending-table-alias) :destination)
                                              ((:fields fields) (lambda (chain-length)
                                                                  (declare (ignore chain-length))
                                                                  :id))
                                              ((:clauses clauses) nil)
                                              ((:union-clauses union-clauses) nil))
  (declare (type boolean backward? closure?)
           (type integer exponent))
  (let* ((source-column (if (not backward?) :source :destination))
         (destination-column (if (not backward?) :destination :source))
         (max-fields-length (length (funcall fields exponent))))
    (labels ((get-padded-fields (chain-length) (let ((current-fields (funcall fields chain-length)))
                                                 (append current-fields
                                                         (loop :for i :from (1+ (length current-fields)) :to max-fields-length
                                                               :collect :null))))
             (build-select (chain-length) (apply #'build :select
                                                 (get-padded-fields chain-length)
                                                 (append
                                                   (zac.aux:get-chained-table-expression chain-length
                                                                                         :note :id
                                                                                         link-name-generator source-column destination-column
                                                                                         :note :id
                                                                                         :starting-table-alias starting-table-alias
                                                                                         :ending-table-alias ending-table-alias)
                                                   clauses)))
             (build-union (max-chain-length) (apply #'build :union-queries
                                                    (append
                                                      (loop :for i :from 1 :to max-chain-length
                                                            :collect (build-select i))
                                                      union-clauses))))
      (funcall (if closure? #'build-union #'build-select) exponent))))


(defun select-notes-through-links (note-id backward? exponent closure?)
  (declare (type integer note-id exponent)
           (type boolean backward? closure?))
  (let* ((target-column (if (not backward?) :destination :source))
         (show-numbers? (and (not closure?)
                             (= exponent 1))))
    (labels ((link-name-gen (&optional index column) (make-name :table :link
                                                                :index index
                                                                :column column))
             (get-fields-for-join-chain (chain-length) (loop :for index :from 0 :to (1- chain-length)
                                                             :collect (link-name-gen index target-column)))
             (fields-generator (chain-length) (list-existing* :destination.text
                                                              :destination.id
                                                              (when show-numbers? (link-name-gen 0 :number))
                                                              (when closure? (get-fields-for-join-chain chain-length)))))
      (let* ((found-rows (query (build-select-notes-through-links :backward backward?
                                                                  :depth exponent
                                                                  :link-name-generator #'link-name-gen
                                                                  :fields #'fields-generator
                                                                  :closure closure?
                                                                  :clauses (list-existing (where (:= :source.id note-id))
                                                                                          (unless closure?
                                                                                            (apply #'build :order-by (list-existing (when show-numbers?
                                                                                                                                      (link-name-gen 0 :number))
                                                                                                                                    :destination.id))))
                                                                  :union-clauses (list-existing (when closure?
                                                                                                  (apply #'build :order-by
                                                                                                         (reverse (get-fields-for-join-chain exponent))))))))
             (transformed-rows (map 'list (lambda (row)
                                            (append (list :text (first row))
                                                    (list :id (second row))
                                                    (when show-numbers? (list :number (third row)))
                                                    (when closure? (list :path (list-existing* (cddr row))))))
                                    found-rows)))
        transformed-rows))))


(defun column-names-for-notes-through-links (backward? exponent closure?)
  (declare (ignore backward?))
  (let ((show-numbers? (and (not closure?)
                            (= exponent 1))))
    (list-existing (when closure? "Path")
                   (second (assoc :text zac.box.db:+table-note-fields+))
                   (when show-numbers? (second (assoc :number zac.box.db:+table-link-fields+))))))


(defun note-path-to-string (path &optional (nil-string "N"))
  (format nil "［箕窿夯镰峻蔺磲ъ轶灬礅溽ㄩ铛祆铋飙篝蜷铉┅疳翳┅ㄤ彐躅蝻鳝趄犷箧矧磲糸镱鏖翳秕舡疳翳轭蝻黧脲è侯蹴忮颦篝踱铛礅弪篝踱è恒镱糸铛瀛篝踱泔铘轭蹂篝踱劲┅磲ъ轶灬礅溽蝻鳗戾舄è轭溴ㄦ轵篝蝻鳗趄犷箧矧礤洵蝻蝈篝蝻鳗铛礅弪ㄧ弭趄犷箧矧礤洵蝻侯蹴忮颟┅扉篝屮轶糸铉轭溴ㄧ弭趄犷箧矧礤洵蝻呼屮舂ㄩ铛礅弪ㄩ铛礅弪癌泔铘轭蹂篝踱铛礅弪铛礅弪篝踱┅┅蝻黧┅ㄤ彐躅蝻鳝趄犷箧矧磲糸镱骘颦疳翳轭蝻黧脲è衡徙膑狎忉汶麽蜾咯铋飑花骘蝽狒⒁献雍窿Β蝻黧戾è殇磲痧轭磲ъ轶灬礅溽蝻鳗戾è轭溴ㄦ轵篝蝻鳗趄犷箧矧礤洵蝻蝈篝蝻鳗┅扉篝ㄧ弭趄犷箧矧礤洵蝻洪洎轭溴┅蝻黧┅花骘蝽狒⑸土泻窿Β殇磲痧轭绌磲ъ轶灬礅溽蝻鳗戾舄è轭溴ㄦ轵篝蝻鳗趄犷箧矧礤洵蝻蝈篝蝻鳗痱弭豉疳翳磲ъ轶灬礅溽铒翦殇箦泔钿ㄡ篌镢铒翦殇殇磲痧轭绌┅ㄧ弭趄犷箧矧礤洵蝻吼狒瑭┅扉篝轭溴铒翦疳翳麸篝蜷铉ㄩ忉汶麽蜾蝈鲥蝮痱弭豉疳翳痱弭豉疳翳┅ㄧ弭趄犷箧矧礤洵蝻呼屮舂┅蝻黧┅ㄤ彐躅汨镲箦蝻鳝骝镯铒翦翳蝻蹒璀扉铍趄犷箧矧礤洵蝻黧痱镯痿忉汶麽蜾屮痫铄铘沆矬躜蹇戾舄è泔祯眍钺礤ㄣ镬蹴瞽钺礤蟓骘颦铒翦蟓翳蝻蹒璀扉铍忉汶麽蜾屮痫铄铘沆矬躜蹇┅ㄣ栾箦瞽蝻鳝轭溴ㄡ钿趄犷箧矧礤洵蝻黧ㄦ轭洵蝻鳝溟犰镧泔祯眍钺礤趄犷箧矧礤洵蝻黧候秣趄犷箧矧磲糸镱骢钽糸镱ㄩ沆矬躜蹇灬礅溽蝻黧蝻鳝趄犷箧矧磲糸镱骘颦疳翳轭蝻黧衡徙膑狎忉汶麽蜾咯＇蝻鳝趄犷箧矧磲糸镱鏖翳秕舡疳翳轭绌虹弭轭溴吼蝻眇舡骢痱镯痿┅┅ㄡ钿汨矬孱蝻鳝轭溴铘汨矬孱蝻鳝轭溴趄犷箧矧礤洵蝻黧┅┅ㄤ彐躅痱弭豉痱轭舡铒翦翳蝻蹒璀扉铍趄犷箧矧礤洵蝻黧忉汶麽蜾屮痫铄铘沆矬躜蹇戾舄è泔祯眍钺礤ㄣ镬蹴瞽钺礤蟓骘颦铒翦蟓翳蝻蹒璀扉铍忉汶麽蜾屮痫铄铘沆矬躜蹇┅蝻鳝趄犷箧矧磲糸镱ㄩ沆矬躜蹇灬礅溽蝻黧蝻鳝趄犷箧矧磲糸镱骘颦疳翳轭蝻黧衡徙膑狎忉汶麽蜾咯＇蝻鳝趄犷箧矧磲糸镱鏖翳秕舡疳翳轭绌痱弭糸骈邃蝻黧磲ъ轶＇沅ㄦ躅汜祆蝻鳝趄犷箧矧磲糸镱祜镳烘矧蝻洪趄犷箧矧礤洵蝻黧烘矧烘蝻麸戾铉翳趄犷箧矧礤洵蝻黧恒镬戾泗ㄣ镱蝻鳗┅┅痱弭豉痱轭舡翎忪泔祯眍钺礤痱弭糸骈邃蝻黧┅