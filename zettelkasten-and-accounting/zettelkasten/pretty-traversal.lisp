;;;; pretty-traversal.lisp


(in-package :zac.zettelkasten.pretty-traversal)


;;; fields is a function of one argument - how long join-chain is supposed to be in this select
(defun build-select-notes-through-links (&key ((:backward backward?) nil)
                                              ((:depth exponent) 1)
                                              ((:closure closure?) nil)
                                              ((:link-name-generator link-name-generator) (lambda (&optional index)
                                                                                            (make-name :table :link
                                                                                                       :index index)))
                                              ((:starting-table-alias starting-table-alias) :source)
                                              ((:ending-table-alias ending-table-alias) :destination)
                                              ((:fields fields) (lambda (chain-length)
                                                                  (declare (ignore chain-length))
                                                                  :id))
                                              ((:clauses clauses) nil)
                                              ((:union-clauses union-clauses) nil))
  (declare (type boolean backward? closure?)
           (type integer exponent))
  (let* ((source-column (if (not backward?) :source :destination))
         (destination-column (if (not backward?) :destination :source))
         (max-fields-length (length (funcall fields exponent))))
    (labels ((get-padded-fields (chain-length) (let ((current-fields (funcall fields chain-length)))
                                                 (append current-fields
                                                         (loop :for i :from (1+ (length current-fields)) :to max-fields-length
                                                               :collect :null))))
             (build-select (chain-length) (apply #'build :select
                                                 (get-padded-fields chain-length)
                                                 (append
                                                   (get-chained-table-expression chain-length
                                                                                 :note :id
                                                                                 link-name-generator source-column destination-column
                                                                                 :note :id
                                                                                 :starting-table-alias starting-table-alias
                                                                                 :ending-table-alias ending-table-alias)
                                                   clauses)))
             (build-union (max-chain-length) (apply #'build :union-queries
                                                    (append
                                                      (loop :for i :from 1 :to max-chain-length
                                                            :collect (build-select i))
                                                      union-clauses))))
      (funcall (if closure? #'build-union #'build-select) exponent))))


(defun select-notes-through-links (note-id &key ((:direction direction)) ((:exponent exponent)) ((:closure closure?)) &allow-other-keys)
  (declare (type integer note-id exponent)
           (type keyword direction)
           (type boolean closure?))
  (let* ((target-column (if (eq direction :forward) :destination :source))
         (backward? (eq direction :backward))
         (show-numbers? (and (not closure?)
                             (= exponent 1))))
    (labels ((link-name-gen (&optional index column) (make-name :table :link
                                                                :index index
                                                                :column column))
             (get-fields-for-join-chain (chain-length) (cons :source.id
                                                             (loop :for index :from 0 :to (1- chain-length)
                                                                   :collect (link-name-gen index target-column))))
             (fields-generator (chain-length) (list-existing* :destination.text
                                                              :destination.id
                                                              (when show-numbers? (link-name-gen 0 :number))
                                                              (when closure? (get-fields-for-join-chain chain-length)))))
      (let* ((found-rows (query (build-select-notes-through-links :backward backward?
                                                                  :depth exponent
                                                                  :link-name-generator #'link-name-gen
                                                                  :fields #'fields-generator
                                                                  :closure closure?
                                                                  :clauses (list-existing (where (:and (:= :source.id note-id)
                                                                                                       (:!= :destination.id note-id)))
                                                                                          (unless closure?
                                                                                            (apply #'build :group-by (fields-generator exponent)))
                                                                                          (unless closure?
                                                                                            (apply #'build :order-by (list-existing (when show-numbers?
                                                                                                                                      (link-name-gen 0 :number))
                                                                                                                                    :destination.id
                                                                                                                                    :destination.text))))
                                                                  :union-clauses (list-existing (when closure?
                                                                                                  (apply #'build :order-by
                                                                                                         (reverse (cons :destination.text
                                                                                                                        (get-fields-for-join-chain exponent)))))))))
             (transformed-rows (map 'list (lambda (row)
                                            (append (list :text (first row))
                                                    (list :id (second row))
                                                    (when show-numbers? (list :number (third row)))
                                                    (when closure? (list :path (list-existing* (cddr row))))))
                                    found-rows)))
        transformed-rows))))


(defun column-names-for-notes-through-links (backward? exponent closure?)
  (declare (ignore backward?))
  (let ((show-numbers? (and (not closure?)
                            (= exponent 1))))
    (list-existing (when closure? "Referrers")
                   (second (assoc :text zac.box.db:+table-note-fields+))
                   (when show-numbers? (second (assoc :number zac.box.db:+table-link-fields+))))))


(defun note-path-to-string (path &optional (nil-string "N"))
  (format nil "［箕窿夯镰峻蔺磲ъ轶灬礅溽ㄩ铛祆铋飙篝蜷铉┅疳翳┅ㄤ彐躅蝻鳝趄犷箧矧磲糸镱鏖翳秕舡疳翳轭蝻黧脲è侯蹴忮颦篝踱铛礅弪篝踱è恒镱糸铛瀛篝踱泔铘轭蹂篝踱劲┅磲ъ轶灬礅溽蝻鳗戾舄è轭溴ㄦ轵篝蝻鳗趄犷箧矧礤洵蝻蝈篝蝻鳗铛礅弪ㄧ弭趄犷箧矧礤洵蝻侯蹴忮颟┅扉篝屮轶糸铉轭溴ㄧ弭趄犷箧矧礤洵蝻呼屮舂ㄩ铛礅弪ㄩ铛礅弪癌泔铘轭蹂篝踱铛礅弪铛礅弪篝踱┅┅蝻黧┅ㄤ彐躅痱弭糸纟蝻黧蝻黧脲è侯蹴忮颦篝踱铛礅弪篝踱è恒镱糸铛瀛篝踱泔铘轭蹂篝踱劲┅磲ъ轶灬礅溽蝻鳗戾è铛礅弪ㄧ弭蝻侯蹴忮颟┅扉篝屮轶糸铉ㄧ弭蝻呼屮舂ㄩ铛礅弪ㄩ铛礅弪癌泔铘轭蹂篝踱铛礅弪铛礅弪篝踱┅┅蝻黧┅ㄤ彐躅沆遽颦泫沆弩骝镯疳翳疳翳祜镳烘矧沲蝌孱舡扉篝疳翳呼桢蝈篝沲蝌孱舡扉篝烘矧沲蝌孱舡殇ㄦ轵篝沲蝌孱舡扉篝烘矧痱弼轱躞扉篝ㄣ镱沲蝌孱舡殇痱弼轱躞扉篝烘矧痫痫箝糸镱沲蝌孱舡殇痱弼轱躞扉篝后翎螋暴乎铎弩沲蝌孱舡扉篝候弭躜铗弼弪箦蝈篝痱弼轱躞扉篝┅瑚桢痫轰箦翩痱弼轱躞扉篝篚怏羼痱弼轱躞扉篝痫螬┅换清蝈驽蝌弪箦泔钿麸灬篝轭疳翳螬换语螋蝻黧怡疳翳戾铉翳换语螋蝈驽蝌弪骘弼弪殇怡矧溴镦蝻黧轭箫螋邃蝻扉篝换徐徙箫螋邃蝈驽蝌弪轭瓠扉篝怡候彐弪蝈蝮脲ㄤ彐躅珏舡蝈驽蝌弪蝻黧戾舄è蝻黧鏖翳沆遽颦疳翳磲ъ轶灬礅溽蝻鳗ㄡ痧孱蝻扉篝恒戾狎疳翳ㄣ戾狎泫沆弩骝镯疳翳ㄧ弭蝻吼狒瑭┅┅蝻黧┅箫螋邃蝻黧箫螋蝻黧鏖翳沆遽颦疳翳＇弘妁灬礅溽蝻鳗戾铉翳ㄧ弭蝻恒戾狎疳翳┅┅蝈驽蝌弪磲脲栳箬翎忪呼弩＇羼飑┅灬忮祗è蝻鳝殇脲蝻鳗ㄧ弭蝻洪洎换阻孱戾浇铂翳疳翳轶沲蝌孱舡铒翦翎蜱弭铒翦换轭翳轶汜箦蝈驽蝌弪轶沲蝌孱舡铒翦ㄩ瀹蝻雉镦翳轶篚怍蝈濠换渝泔钿麸灬篝忮汜躞翳灬篝镱轶翎蜱弭铒翦殇怩麇铄邃蝈驽蝌弪殇ㄧ弭蝈驽蝌弪骝镯疳翳疳翳ㄩ戾铉翳疳翳博ㄦ轵篝灬篝疳翳博蝻雉┅换珏铄蜥翦蝈驽蝌弪扉篝鏖翳箫螋邃蝈驽蝌弪换箫螋邃蝻黧犰蝈徜弩翎忪轶桢箫礤箫螋镦箫螋轭换牾篝栳蜾泔瘗轸躞轭痫箝糸镱镦殇轭箝溴箫螋邃蝻黧骘泔眇狎轶镱换篚怍蝈蝻雉轶犰麽镱麸换紊腆狎殇镦铒翦蟋翳狒麇蝈骈祠弪邃漉麸箫礤汩蜚蹴篝犷沐换翳妁狎犰麽狒翳怙趑镯ㄧ孱弪狒瀛蝈驽蝌弪扉篝蝻鳗箫螋蝈盹鲥漉痨殂狒弩ㄧ弭栳箬蝻鳝殇脲蝻鳗蝈驽蝌弪螬灬礅溽镱犷雉桢颟ㄩㄥ犷雉桢蝻雉铋ㄩㄥ镱蝻雉戾è镱瀛痫痫箝糸镱镱箫螋邃蝻黧弘妁＇蝻鳝殇脲┅ㄡ铒翳弪痫痫箝糸镱犷雉桢箫螋邃蝻黧弘妁＇蝻鳝殇脲┅ㄣ镱è犷铛祆镱瀛痫螬铛祆犷雉桢颦痫螬铋飑è犷铛祆镱瀛痫螬犷雉桢颦痫螬铋飑è犷镱瀛痫铛祆犷雉桢颦痫螬舂ê屐箦镱瀛痫犷雉桢颦痫螬┅┅┅┅祜镳烘矧蝻洪箫螋邃蝻黧轰瘐箬铄ㄧ弭蝈驽蝌弪骝镯疳翳ㄧ弭蝻恒戾狎疳翳┅ㄧ弭栳箬蝻鳝殇脲蝻鳗蝈驽蝌弪螬┅磲ъ轶灬礅溽蝻鳗ㄡ痧孱蝻扉篝候彐弪蝈蝮ㄧ孱弪狒瀛蝈驽蝌弪扉篝蝻鳗┅蝈盹鲥漉痨殂狒弩箫螋邃蝻黧弘妁＇蝻鳝殇脲┅┅ㄤ彐躅蝈驽蝌弪蟓麸篝蜷铉蝈驽蝌弪扉篝脲è后秕蜚瀛篝踱箫躜沐篝踱⒕è侯镱屮轶翦铘蝈驽蝌弪篝踱铒铄轶翦铘蝈驽蝌弪篝踱┅ㄦ矧磲铋［箕窿夯蔺磲ъ轶灬礅溽蝈驽蝌弪ㄣ镱è羼蝈驽蝌弪蝻雉箫躜沐篝踱è铛祆蝈驽蝌弪铒铄轶翦铘蝈驽蝌弪篝踱ê屐箦蝈驽蝌弪┅蝈驽蝌弪扉篝┅ㄤ彐躅蝻鳝趄犷箧矧磲糸镱蟓骘颦蝈驽蝌弪蝻黧戾è殇磲痧轭磲ъ轶灬礅溽蝻鳗戾è轭溴ㄦ轵篝蝻鳗趄犷箧矧礤洵蝻蝈篝蝻鳗┅扉篝ㄧ弭趄犷箧矧礤洵蝻洪洎轭溴┅蝻黧┅磲ъ轶灬礅溽蝻鳗戾舄è轭溴ㄦ轵篝蝻鳗趄犷箧矧礤洵蝻蝈篝蝻鳗痱弭豉蝈驽蝌弪磲ъ轶灬礅溽蝈驽蝌弪ㄩㄥ蝈驽蝌弪蝻雉蝈驽蝌弪箦泔钿ㄡ篌镢蝈驽蝌弪殇磲痧轭绌┅ㄧ弭趄犷箧矧礤洵蝻候彐弪蝈蝮┅┅扉篝轭溴蝈驽蝌弪蟓麸篝蜷铉痱弭豉蝈驽蝌弪螬ㄧ弭趄犷箧矧礤洵蝻呼屮舂┅蝻黧┅ㄤ彐躅汨镲箦蝻鳝骝镯铒翦翳蝻蹒璀扉铍趄犷箧矧礤洵蝻黧痱镯痿脲è轰轵邈糸镱溟蝈泗轱瞟è哄痫铄铘屮痫铄铘┅è恒祜篚蝈沆矬躜蹇┅è恒栾矬瀛磲铢汨镲箦磲铢铋飑犰祜鳝雉桢颦脲螬ㄤ邈灬蝈豉疱轭翦珏铒翦殇屮痫铄铘豉疱脲黠蜾溟蝈泗轱瞟豉疱怙镬遽沆矬躜蹇┅戾舄è忉汶麽蜾ㄥ溟蝈泗轱衡徙膑狎洎┅ㄣ镱ㄣ祜篚蝈戾舄è泔祯眍钺礤ㄣ镬蹴瞽钺礤蟓骘颦铒翦蟓翳蝻蹒璀扉铍忉汶麽蜾屮痫铄铘沆矬躜蹇┅箫螋邃蝻黧ㄧ弭蝈驽蝌弪趄犷箧矧礤洵蝻黧┅ㄣ栾箦瞽蝻鳝轭溴ㄡ钿箫螋邃蝻黧ㄦ轭洵蝻鳝溟犰镧泔祯眍钺礤箫螋邃蝻黧候秣趄犷箧矧磲糸镱骢钽糸镱＇蝻鳝趄犷箧矧磲糸镱蟓骘颦蝈驽蝌弪虹弭轭溴吼蝻眇舡骢痱镯痿恒栾矬瀛磲铢汨镲箦磲铢┅┅ㄡ钿汨矬孱蝻鳝轭溴ㄩ汨镲箦磲铢祜镳烘矧轭溴洪汨矬孱蝻鳝轭溴恒镬戾泗铘轭溴箫螋邃蝻黧┅铘汨矬孱蝻鳝轭溴箫螋邃蝻黧┅┅ê屐箦戾舄è泔祯眍钺礤ㄣ镬蹴瞽钺礤蟓骘颦铒翦蟓翳蝻蹒璀扉铍忉汶麽蜾屮痫铄铘沆矬躜蹇┅ㄣ栾箦瞽蝻鳝轭溴ㄡ钿趄犷箧矧礤洵蝻黧ㄦ轭洵蝻鳝溟犰镧泔祯眍钺礤痱弭糸纟蝻黧趄犷箧矧礤洵蝻黧虹弭轭溴吼蝻眇舡骢痱镯痿恒栾矬瀛磲铢汨镲箦磲铢┅┅ㄡ钿汨矬孱蝻鳝轭溴ㄩ汨镲箦磲铢祜镳烘矧轭溴洪汨矬孱蝻鳝轭溴恒镬戾泗铘轭溴趄犷箧矧礤洵蝻黧┅铘汨矬孱蝻鳝轭溴趄犷箧矧礤洵蝻黧┅┅┅┅ㄤ彐躅痱弭豉痱轭舡铒翦翳蝻蹒璀扉铍趄犷箧矧礤洵蝻黧脲è轰轵邈糸镱溟蝈泗轱瞟è哄痫铄铘屮痫铄铘┅è恒祜篚蝈沆矬躜蹇┅犰祜鳝雉桢颦脲螬戾舄è忉汶麽蜾ㄥ溟蝈泗轱衡徙膑狎洎ㄣ镬蹴瞽钺礤ㄣ镬蹴瞽钺礤蟓骘颦铒翦蟓翳蝻蹒璀扉铍忉汶麽蜾屮痫铄铘沆矬躜蹇┅箫螋邃蝻黧ㄦ躅汜祆ㄩ沆矬躜蹇＇珏舡蝈驽蝌弪＇痱弭糸纟蝻黧趄犷箧矧礤洵蝻黧┅痱弭糸骈邃蝻黧ㄩ沆矬躜蹇蝻鳝趄犷箧矧磲糸镱蟓骘颦蝈驽蝌弪祜镳烘矧蝻洪箫螋邃蝻黧烘矧烘蝻麸戾铉翳箫螋邃蝻黧恒镬戾泗ㄣ镱蝻鳗┅箫螋邃蝻黧┅痱弭豉痱轭舡翎忪ㄣ镱⑽蹴忮颌泔祯眍钺礤螬痱弭糸骈邃蝻黧┅